## ORCHESTRATOR INFERENCE ROLE
# Primary execution role for the Onward Journey Orchestration Layer.

resource "aws_iam_role" "inference" {
  name               = "${var.environment}-inference-role"
  assume_role_policy = data.aws_iam_policy_document.allow_all_assume_role.json
}

data "aws_iam_policy_document" "allow_all_assume_role" {
  statement {
    sid = "AllowLambdaAndUsersToAssumeRole"

    actions = [
      "sts:AssumeRole"
    ]

    # This allows the Lambda Service to assume the role
    principals {
      type        = "Service"
      identifiers = ["lambda.amazonaws.com"]
    }

    # This allows local devs to assume the role for testing
    principals {
      type = "AWS"
      identifiers = [
        "arn:aws:iam::${var.aws_account_id}:root"
      ]
    }
  }
}

## INFERENCE ROLE ATTACHMENTS
# AWS managed policies and specific AgentCore access requirements.

# Grants the Orchestration Layer permission to create Network Interfaces within the VPC.
resource "aws_iam_role_policy_attachment" "lambda_vpc_access" {
  role       = aws_iam_role.inference.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

# Use AWS provided "Bedrock Limited Access" policy.
resource "aws_iam_role_policy_attachment" "inference_bedrock_access" {
  role       = aws_iam_role.inference.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonBedrockLimitedAccess"
}

# Permissions for the agent to interact with Amazon Bedrock modular capabilities (AgentCore).
resource "aws_iam_policy" "agentcore_access" {
  name        = "${var.environment}-agentcore-access"
  description = "Allows the Orchestration Layer to use managed Memory and Gateway modules."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "MemoryAndGatewayOps"
        Effect = "Allow"
        Action = [
          # --- AGENTCORE MEMORY: SESSION & CONTEXT ---
          # Essential for LangGraph to save and retrieve conversation turns.
          "bedrock-agentcore:CreateSession",
          "bedrock-agentcore:GetSession",
          "bedrock-agentcore:CreateEvent",      # Action to save a turn to short-term memory
          "bedrock-agentcore:GetMemory",        # Action to retrieve tactical/strategic memory records
          "bedrock-agentcore:ListEvents",       # Required to list the checkpoint history
          "bedrock-agentcore:RetrieveMemories", # Required for more advanced context retrieval

          # --- AGENTCORE RUNTIME ---
          "bedrock:InvokeAgent",
          "bedrock-agentcore:InvokeAgentRuntime",
          "bedrock-agentcore:InvokeGateway", # Required for MCP POST calls
        ]
        Resource = [
          aws_bedrockagentcore_memory.agent_chat_context.arn,
          # The Gateway itself (for management)
          aws_bedrockagentcore_gateway.tool_interface.gateway_arn,
          # The Gateway sub-resources (for /runtime-endpoint/DEFAULT)
          "${aws_bedrockagentcore_gateway.tool_interface.gateway_arn}/*",
          # Identity boundary for agent execution
          "arn:aws:bedrock:${var.aws_region}:${var.aws_account_id}:agent-alias/*"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "inference_agentcore" {
  role       = aws_iam_role.inference.name
  policy_arn = aws_iam_policy.agentcore_access.arn
}

## ORCHESTRATOR RDS & SECRETS ACCESS
# Allows the Orchestrator to fetch the DB password and connect to the RDS instance.

resource "aws_iam_policy" "orchestrator_rds_secrets" {
  name        = "${var.environment}-orchestrator-rds-secrets"
  description = "Allows the Orchestrator to access RDS credentials and Bedrock streaming."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "BedrockStreamingAccess"
        Effect = "Allow"
        Action = [
          "bedrock:InvokeModel",
          "bedrock:InvokeModelWithResponseStream"
        ]
        Resource = [
          # Permission for the base models
          "arn:aws:bedrock:${var.aws_region}::foundation-model/anthropic.claude-*",
          # Permission for the Inference Profiles
          "arn:aws:bedrock:${var.aws_region}:${var.aws_account_id}:inference-profile/eu.anthropic.claude-*",
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "inference_rds_secrets" {
  role       = aws_iam_role.inference.name
  policy_arn = aws_iam_policy.orchestrator_rds_secrets.arn
}

## DATASET ACCESS
# Permissions for reading source data from S3.

resource "aws_iam_policy" "dataset_read" {
  name        = "${var.environment}-dataset-read"
  description = "Allow read access to the dataset s3 bucket"
  policy      = data.aws_iam_policy_document.dataset_read.json
}

data "aws_iam_policy_document" "dataset_read" {
  statement {
    sid     = "ListBucket"
    actions = ["s3:ListBucket"]

    resources = [aws_s3_bucket.dataset_storage.arn]
  }

  statement {
    sid     = "ReadBucketObjects"
    actions = ["s3:GetObject"]

    resources = ["${aws_s3_bucket.dataset_storage.arn}/*"]
  }
}

resource "aws_iam_role_policy_attachment" "inference_allow_dataset_read" {
  role       = aws_iam_role.inference.name
  policy_arn = aws_iam_policy.dataset_read.arn
}


## BEDROCK AGENTCORE SERVICE ROLE
# Required for managed session memory and tool gateway connectivity.

resource "aws_iam_role" "agentcore_role" {
  name = "${var.environment}-agentcore-service-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = {
        Service = [
          "bedrock-agentcore.amazonaws.com",
          "bedrock.amazonaws.com"
        ]
      }
    }]
  })

  tags = {
    Name = "${var.environment}-agentcore-service-role"
  }
}

# Authorizes the Gateway to trigger the RDS Tool Lambda
resource "aws_iam_role_policy" "agentcore_gateway_invocation" {
  name = "${var.environment}-agentcore-gateway-invocation"
  role = aws_iam_role.agentcore_role.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AllowGatewayToInvokeTools"
        Effect = "Allow"
        Action = ["lambda:InvokeFunction"]
        Resource = [
          aws_lambda_function.rds_tool.arn,
          aws_lambda_function.crm_tool.arn
        ]
      }
    ]
  })
}


## BEDROCK AGENTCORE RESOURCE-BASED POLICY
# Explicitly authorises the Bedrock service to invoke the RDS Tool Lambda.
# This acts as the "Resource-Based Policy" on the Lambda side.

resource "aws_lambda_permission" "allow_bedrock_gateway" {
  statement_id  = "AllowBedrockGatewayInvoke"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.rds_tool.function_name
  principal     = "bedrock-agentcore.amazonaws.com"
  source_arn    = aws_bedrockagentcore_gateway.tool_interface.gateway_arn
}

## BEDROCK AGENTCORE RESOURCE-BASED POLICY (CRM)
resource "aws_lambda_permission" "allow_bedrock_gateway_crm" {
  statement_id  = "AllowBedrockGatewayInvokeCRM"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.crm_tool.function_name
  principal     = "bedrock-agentcore.amazonaws.com"
  source_arn    = aws_bedrockagentcore_gateway.tool_interface.gateway_arn
}

## RDS TOOL SERVICE ROLE
# Execution role for the MCP Tool Lambda that handles database searches.
resource "aws_iam_role" "rds_tool_role" {
  name               = "${var.environment}-rds-tool-role"
  assume_role_policy = data.aws_iam_policy_document.allow_all_assume_role.json
}

# Attachment: Reuse VPC access for private RDS connectivity
resource "aws_iam_role_policy_attachment" "rds_tool_vpc_access" {
  role       = aws_iam_role.rds_tool_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

resource "aws_iam_policy" "rds_tool_permissions" {
  name        = "${var.environment}-rds-tool-permissions"
  description = "Provides the RDS Tool access to embedding models and DB credentials. Restricted to access/query only."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid      = "BedrockEmbeddingInvoke"
        Effect   = "Allow"
        Action   = ["bedrock:InvokeModel"]
        Resource = "arn:aws:bedrock:eu-west-2::foundation-model/amazon.titan-embed-text-v2:0"
      },
      {
        Sid    = "RDSIAMConnect"
        Effect = "Allow"
        Action = ["rds-db:connect"]
        Resource = [
          "arn:aws:rds-db:${var.aws_region}:${var.aws_account_id}:dbuser:${aws_db_instance.dept_contacts_metadata.resource_id}/rds_readonly_dept_contacts"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "rds_tool_main" {
  role       = aws_iam_role.rds_tool_role.name
  policy_arn = aws_iam_policy.rds_tool_permissions.arn
}

## CRM TOOL SERVICE ROLE
# Execution role for the MCP Tool Lambda that handles CRM API interactions.

resource "aws_iam_role" "crm_tool_role" {
  name               = "${var.environment}-crm-tool-role"
  assume_role_policy = data.aws_iam_policy_document.allow_all_assume_role.json
}

resource "aws_iam_policy" "crm_tool_permissions" {
  name        = "${var.environment}-crm-tool-permissions"
  description = "Allows the CRM Tool to fetch OAuth credentials prefixed with its environment and crm-creds/. Can log to CloudWatch."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid      = "CRMSecretAccess"
        Effect   = "Allow"
        Action   = ["secretsmanager:GetSecretValue"]
        Resource = "arn:aws:secretsmanager:${var.aws_region}:${var.aws_account_id}:secret:${var.environment}/crm-creds/*"
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "crm_tool_main" {
  role       = aws_iam_role.crm_tool_role.name
  policy_arn = aws_iam_policy.crm_tool_permissions.arn
}

## RDS SEEDER SERVICE ROLE
# Execution role for the Lambda responsible for database initialisation and data loading.

resource "aws_iam_role" "rds_seeder_role" {
  name = "${var.environment}-rds-seeder-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = {
        Service = "lambda.amazonaws.com"
      }
    }]
  })
}

## RDS SEEDER DATA INGESTION POLICY
# Specific permissions for S3 data retrieval, Bedrock model invocation, and secret decryption.

resource "aws_iam_policy" "rds_seeder_permissions" {
  name        = "${var.environment}-rds-seeder-permissions"
  description = "Provides the RDS Seeder access to source datasets, embedding models, and credentials."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid      = "S3DatasetRead"
        Effect   = "Allow"
        Action   = ["s3:GetObject"]
        Resource = "${aws_s3_bucket.dataset_storage.arn}/*"
      },
      {
        Sid      = "BedrockEmbeddingInvoke"
        Effect   = "Allow"
        Action   = ["bedrock:InvokeModel"]
        Resource = "arn:aws:bedrock:eu-west-2::foundation-model/amazon.titan-embed-text-v2:0"
      },
      {
        Sid      = "SecretsManagerAccess"
        Effect   = "Allow"
        Action   = ["secretsmanager:GetSecretValue"]
        Resource = data.aws_secretsmanager_secret_version.dept_contacts_db_password.arn
      },
      {
        Sid      = "CrmToolInvocation"
        Effect   = "Allow"
        Action   = ["lambda:InvokeFunction"]
        Resource = aws_lambda_function.crm_tool.arn
      }
    ]
  })
}

## RDS SEEDER ATTACHMENTS
# Links the ingestion and VPC network policies to the Seeder execution role.

# Attaches the custom data ingestion policy (S3, Bedrock, Secrets).
resource "aws_iam_role_policy_attachment" "rds_seeder_main" {
  role       = aws_iam_role.rds_seeder_role.name
  policy_arn = aws_iam_policy.rds_seeder_permissions.arn
}

# Attaches the managed policy required for private database connectivity.
resource "aws_iam_role_policy_attachment" "rds_seeder_vpc_access" {
  role       = aws_iam_role.rds_seeder_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

## RDS INIT SERVICE ROLE
# Minimal execution role for the Knowledge Base infrastructure initialisation.

resource "aws_iam_role" "rds_init_role" {
  name = "${var.environment}-rds-init-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = {
        Service = "lambda.amazonaws.com"
      }
    }]
  })
}

resource "aws_iam_policy" "rds_init_permissions" {
  name        = "${var.environment}-rds-init-permissions"
  description = "Provides minimal permissions for RDS initialisation (Logs and DB Secrets)."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid    = "SecretsManagerAccess"
        Effect = "Allow"
        Action = ["secretsmanager:GetSecretValue"]
        Resource = [
          data.aws_secretsmanager_secret_version.dept_contacts_db_password.arn
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "rds_init_main" {
  role       = aws_iam_role.rds_init_role.name
  policy_arn = aws_iam_policy.rds_init_permissions.arn
}

resource "aws_iam_role_policy_attachment" "rds_init_vpc_access" {
  role       = aws_iam_role.rds_init_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

## KB SYNC SERVICE ROLE
# Execution role for the Knowledge Base synchronisation pipeline.

resource "aws_iam_role" "kb_sync_role" {
  name               = "${var.environment}-kb-sync-role"
  assume_role_policy = data.aws_iam_policy_document.allow_all_assume_role.json
}

resource "aws_iam_policy" "kb_sync_permissions" {
  name        = "${var.environment}-kb-sync-permissions"
  description = "Provides the KB Sync Pipeline access to embedding models and DB credentials (IAM and Password)."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid      = "BedrockEmbeddingInvoke"
        Effect   = "Allow"
        Action   = ["bedrock:InvokeModel"]
        Resource = "arn:aws:bedrock:eu-west-2::foundation-model/amazon.titan-embed-text-v2:0"
      },
      {
        Sid    = "RDSIAMConnect"
        Effect = "Allow"
        Action = ["rds-db:connect"]
        Resource = [
          "arn:aws:rds-db:${var.aws_region}:${var.aws_account_id}:dbuser:${aws_db_instance.dept_contacts_metadata.resource_id}/rds_readonly_dept_contacts"
        ]
      },
      {
        Sid    = "SecretsManagerAccess"
        Effect = "Allow"
        Action = ["secretsmanager:GetSecretValue"]
        Resource = [
          data.aws_secretsmanager_secret_version.dept_contacts_db_password.arn
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "kb_sync_main" {
  role       = aws_iam_role.kb_sync_role.name
  policy_arn = aws_iam_policy.kb_sync_permissions.arn
}

resource "aws_iam_role_policy_attachment" "kb_sync_vpc_access" {
  role       = aws_iam_role.kb_sync_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

## KB SYNC CRM ROLE
# Execution role for the public-facing components of the KB Sync pipeline (Internet Access).

resource "aws_iam_role" "kb_sync_crm_role" {
  name               = "${var.environment}-kb-sync-crm-role"
  assume_role_policy = data.aws_iam_policy_document.allow_all_assume_role.json
}

resource "aws_iam_policy" "kb_sync_crm_permissions" {
  name        = "${var.environment}-kb-sync-crm-permissions"
  description = "Allows the KB Sync pipeline to fetch external CRM credentials."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "CloudWatchLogAccess"
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = "arn:aws:logs:*:*:*"
      },
      {
        Sid      = "CRMSecretAccess"
        Effect   = "Allow"
        Action   = ["secretsmanager:GetSecretValue"]
        Resource = "arn:aws:secretsmanager:${var.aws_region}:${var.aws_account_id}:secret:${var.environment}/crm-creds/*"
      }
    ]
  })
}

## KB SYNC CLEANUP: IAM ROLE & POLICIES
resource "aws_iam_role" "kb_sync_cleanup_role" {
  name = "${var.environment}-kb-sync-cleanup-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      }
    ]
  })
}

# Attach standard VPC execution role (provides ENI creation for VPC and CloudWatch logging)
resource "aws_iam_role_policy_attachment" "kb_sync_cleanup_vpc_access" {
  role       = aws_iam_role.kb_sync_cleanup_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

# Explicitly allow reading the DB password secret
resource "aws_iam_policy" "kb_sync_cleanup_secrets_policy" {
  name        = "${var.environment}-kb-sync-cleanup-secrets-policy"
  description = "Allows KB Sync cleanup lambda to fetch RDS credentials"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "secretsmanager:GetSecretValue"
        ]
        Resource = [
          data.aws_secretsmanager_secret_version.dept_contacts_db_password.arn
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "kb_sync_cleanup_secrets_attach" {
  role       = aws_iam_role.kb_sync_cleanup_role.name
  policy_arn = aws_iam_policy.kb_sync_cleanup_secrets_policy.arn
}



resource "aws_iam_role_policy_attachment" "kb_sync_crm_main" {
  role       = aws_iam_role.kb_sync_crm_role.name
  policy_arn = aws_iam_policy.kb_sync_crm_permissions.arn
}

## UNAUTHENTICATED IDENTITY ROLE
# The principal here is "cognito-identity.amazonaws.com", scoped to this pool
# and to the unauthenticated context only.

data "aws_iam_policy_document" "cognito_anon_assume" {
  statement {
    sid     = "CognitoUnauthAssumeRole"
    actions = ["sts:AssumeRoleWithWebIdentity"]

    principals {
      type        = "Federated"
      identifiers = ["cognito-identity.amazonaws.com"]
    }

    condition {
      test     = "StringEquals"
      variable = "cognito-identity.amazonaws.com:aud"
      values   = [aws_cognito_identity_pool.frontend_anon.id]
    }

    condition {
      test     = "ForAnyValue:StringLike"
      variable = "cognito-identity.amazonaws.com:amr"
      values   = ["unauthenticated"]
    }
  }
}

resource "aws_iam_role" "cognito_anon_role" {
  name               = "${var.environment}-cognito-anon-role"
  assume_role_policy = data.aws_iam_policy_document.cognito_anon_assume.json
}

## INVOKE PERMISSION
# Grants only lambda:InvokeFunctionUrl on the specific Orchestrator Lambda.

resource "aws_iam_policy" "cognito_anon_invoke" {
  name        = "${var.environment}-cognito-anon-invoke"
  description = "Allows Cognito unauthenticated identities to invoke the Orchestrator Lambda URL."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid      = "InvokeOrchestratorUrl"
        Effect   = "Allow"
        Action   = "lambda:InvokeFunctionUrl"
        Resource = aws_lambda_function.orchestrator.arn
        Condition = {
          StringEquals = {
            "lambda:FunctionUrlAuthType" = "AWS_IAM"
          }
        }
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "cognito_anon_invoke" {
  role       = aws_iam_role.cognito_anon_role.name
  policy_arn = aws_iam_policy.cognito_anon_invoke.arn
}

resource "time_sleep" "wait_for_iam_propagation" {
  create_duration = "30s"

  depends_on = [
    aws_iam_role_policy.agentcore_gateway_invocation,
  ]
}

# -------------------------------------------------------------------------
# AGENTCORE RUNTIME ROLES
# -------------------------------------------------------------------------

resource "aws_iam_role" "agentcore_runtime_execution_role" {
  name = "${var.environment}-agentcore-runtime-execution-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "bedrock-agentcore.amazonaws.com"
        }
      }
    ]
  })
}

# Grants explicit, least-privilege permission to invoke specific tools via the Gateway.
resource "aws_iam_policy" "agentcore_runtime_gateway_invoke" {
  name        = "${var.environment}-agentcore-runtime-gateway-invoke-policy"
  description = "Allows AgentCore Runtime Orchestrator to invoke specific tools via the Gateway"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = "bedrock-agentcore:InvokeGateway"
        # Explicitly restrict access to only the CRM and RDS paths
        Resource = [
          "arn:aws:bedrock-agentcore:${var.aws_region}:${var.aws_account_id}:gateway/${aws_bedrockagentcore_gateway.tool_interface.gateway_id}",
          "arn:aws:bedrock-agentcore:${var.aws_region}:${var.aws_account_id}:gateway/${aws_bedrockagentcore_gateway.tool_interface.gateway_id}/target/${aws_bedrockagentcore_gateway_target.rds_search_tool.name}",
          "arn:aws:bedrock-agentcore:${var.aws_region}:${var.aws_account_id}:gateway/${aws_bedrockagentcore_gateway.tool_interface.gateway_id}/target/${aws_bedrockagentcore_gateway_target.crm_availability.name}",
          "arn:aws:bedrock-agentcore:${var.aws_region}:${var.aws_account_id}:gateway/${aws_bedrockagentcore_gateway.tool_interface.gateway_id}/target/${aws_bedrockagentcore_gateway_target.crm_handoff.name}"
        ]
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "agentcore_runtime_gateway_attach" {
  role       = aws_iam_role.agentcore_runtime_execution_role.name
  policy_arn = aws_iam_policy.agentcore_runtime_gateway_invoke.arn
}

# Required to allow AgentCore Runtime to deploy Elastic Network Interfaces (ENIs) into private subnets
resource "aws_iam_role_policy_attachment" "agentcore_runtime_vpc_attach" {
  role       = aws_iam_role.agentcore_runtime_execution_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

# Grants the AgentCore Runtime permission to read/write conversation history and invoke Bedrock models
resource "aws_iam_policy" "agentcore_runtime_memory_and_inference" {
  name        = "${var.environment}-agentcore-runtime-memory-inference-policy"
  description = "Allows AgentCore to access memory and invoke Claude strictly within the EU"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "MemoryAccess"
        Effect = "Allow"
        Action = [
          "bedrock-agentcore:ListEvents",
          "bedrock-agentcore:GetEvent",
          "bedrock-agentcore:PutEvent",
          "bedrock-agentcore:CreateEvent",
          "bedrock-agentcore:GetMemory"
        ]
        Resource = [
          aws_bedrockagentcore_memory.agent_chat_context.arn
        ]
      },
      {
        Sid    = "BedrockModelInvoke"
        Effect = "Allow"
        Action = [
          "bedrock:InvokeModel",
          "bedrock:InvokeModelWithResponseStream"
        ]
        Resource = concat(
          # Dynamically generates strict ARNs for the foundation model in ONLY the allowed EU regions
          [for region in local.eu_inference_regions : "arn:aws:bedrock:${region}::foundation-model/anthropic.claude-*"],

          # Allows access to the EU geographic inference profile in your source region
          ["arn:aws:bedrock:${var.aws_region}:${var.aws_account_id}:inference-profile/eu.anthropic.claude-*"]
        )
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "agentcore_runtime_memory_inference_attach" {
  role       = aws_iam_role.agentcore_runtime_execution_role.name
  policy_arn = aws_iam_policy.agentcore_runtime_memory_and_inference.arn
}
